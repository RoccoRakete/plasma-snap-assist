{
  description = "Snap Assist: KWin script that suggests windows to fill the free space after quick-tiling";

  inputs.nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";

  outputs =
    { self, nixpkgs }:
    let
      systems = [
        "x86_64-linux"
        "aarch64-linux"
      ];
      forAllSystems = f: nixpkgs.lib.genAttrs systems (system: f nixpkgs.legacyPackages.${system});
    in
    {
      packages = forAllSystems (pkgs: {
        default = self.packages.${pkgs.stdenv.hostPlatform.system}.snapassist;
        snapassist = pkgs.stdenvNoCC.mkDerivation {
          pname = "snapassist";
          version =
            (builtins.fromJSON (builtins.readFile ./kwin-script/org.kde.snapassist/metadata.json))
            .KPlugin.Version;
          src = ./.;

          # KWin finds scripts in $XDG_DATA_DIRS/kwin/scripts/<Id> and scripted
          # effects in $XDG_DATA_DIRS/kwin/effects/<Id>. The effect is disabled
          # by default, so shipping it only makes it selectable in Desktop Effects.
          installPhase = ''
            runHook preInstall
            mkdir -p $out/share/kwin/scripts/org.kde.snapassist $out/share/kwin/effects/org.kde.snapassist.placementeffect
            cp -r kwin-script/org.kde.snapassist/{metadata.json,contents} $out/share/kwin/scripts/org.kde.snapassist/
            cp -r effect/org.kde.snapassist.placementeffect/{metadata.json,contents} $out/share/kwin/effects/org.kde.snapassist.placementeffect/
            runHook postInstall
          '';

          meta = {
            description = "KWin script that suggests windows to fill the free space after quick-tiling";
            license = pkgs.lib.licenses.gpl2Plus;
            platforms = pkgs.lib.platforms.linux;
          };
        };
      });

      checks = forAllSystems (pkgs: {
        tests =
          pkgs.runCommand "snapassist-tests"
            {
              nativeBuildInputs = [ pkgs.kdePackages.qtdeclarative ];
              QT_QPA_PLATFORM = "offscreen";
              QT_PLUGIN_PATH = "${pkgs.kdePackages.qtbase}/${pkgs.kdePackages.qtbase.qtPluginPrefix}";
            }
            ''
              export XDG_CACHE_HOME=$TMPDIR
              # The tests import ../contents/ui/*.js, so keep the package layout.
              qmltestrunner \
                -import ${pkgs.kdePackages.qtdeclarative}/${pkgs.kdePackages.qtbase.qtQmlPrefix} \
                -input ${./kwin-script/org.kde.snapassist}/tests
              touch $out
            '';
      });

      formatter = forAllSystems (pkgs: pkgs.nixfmt);
    };
}
